/*
 * Copyright 2015 Palantir Technologies, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        maven {
            url 'http://dl.bintray.com/palantir/releases'
        }

        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.netflix.nebula:nebula-project-plugin:2.2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'nebula-integtest'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'distribution'

repositories {
    maven {
        url 'http://dl.bintray.com/palantir/releases'
    }

    jcenter()
    mavenCentral()
}

dependencies {
    testCompile('com.netflix.nebula:nebula-test:2.2.0') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    integTestCompile 'com.palantir:gradle-grunt-plugin:0.10-FORK'
    integTestCompile 'com.palantir:gradle-gulp-plugin:0.10-FORK'
}

group 'com.palantir'

ext {
    bintrayUser = System.env.'BINTRAY_USER'
    bintrayKey = System.env.'BINTRAY_KEY'
    travisTag = System.env.'TRAVIS_TAG'
}

tasks.withType(Tar) {
    compression Compression.GZIP
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task ci {
    dependsOn 'build'
    if (travisTag ==~ /\d+\.\d+\.\d+/ && bintrayUser != null && bintrayKey != null) {
        dependsOn 'bintrayUpload'
    }
}

bintrayUpload.dependsOn 'check'

distTar.dependsOn 'publishMavenJavaPublicationToMavenRepository'
distZip.dependsOn 'publishMavenJavaPublicationToMavenRepository'

distributions {
    main {
        contents {
            from ("${buildDir}/localpublish") {
                include '**/*'
                exclude '**/maven-metadata.xml*'
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }

    repositories {
        maven {
            url "$buildDir/localpublish/"
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey

    publications = ['mavenJava']
    dryRun = false
    publish = true

    pkg {
        userOrg = 'palantir'
        repo = 'releases'
        name = 'gradle-bowerdeps-plugin'
        desc = 'A Gradle plugin for controlling multiproject build order with bower.json files.'
        websiteUrl = 'https://github.com/palantir/gradle-bowerdeps-plugin'
        issueTrackerUrl = 'https://github.com/palantir/gradle-bowerdeps-plugin/issues'
        vcsUrl = 'https://github.com/palantir/gradle-bowerdeps-plugin.git'
        licenses = ['Apache-2.0']
        labels = ['gradle', 'grunt', 'gulp', 'bower']
        publicDownloadNumbers = true
    }
}
